       IDENTIFICATION                  DIVISION.
      *================================================================*
       PROGRAM-ID.                     F2403DB3.
      *================================================================*
      * PROGRAMA   : F2403DB3
      * PROGRAMADOR: MATHEUS H MEDEIROS
      * ANALISTA   : IVAN SANCHES
      * CONSULTORIA: FOURSYS
      * DATA.......: 23 / 09 / 2024
      *----------------------------------------------------------------*
      * OBJETIVO...: LEITURA DE TODOS OS REGISTROS DA TABELA DB2
      *
      *----------------------------------------------------------------*
      * BASE DE DADOS...:
      * TABELAS.........:                                INCLUDE/BOOKS
      *
      *  F2403.FUNC                                           SQLCA
      *                                                      BOOKFUNC
      *----------------------------------------------------------------*
      * ARQUIVOS...:
      * DDNAME.....:                I/O                  INCLUDE/BOOKS
      *
      *----------------------------------------------------------------*
      * INC'S......:
      * BOOKERRO -> MENSAGENS DE ERRO
      *
      * BOOKLOG  ->  GRAVALOG
      *
      *----------------------------------------------------------------*
      * MODULOS...:
      * GRAVALOG -> FORMATACAO E GRAVACAO DE ERROS
      *
      *================================================================*
       ENVIRONMENT                     DIVISION.
      *================================================================*
       CONFIGURATION                   SECTION.
      *================================================================*
       DATA                            DIVISION.
      *================================================================*
       WORKING-STORAGE                 SECTION.
      *================================================================*
       01  FILLER                      PIC X(050)          VALUE
           '***** INICIO DA WORKING *****'.
      *================================================================*

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '      VARIAVEIS DE APOIO     '.
       01  WRK-ID                       PIC 9(005)          VALUE ZEROS.
       01  WRK-SQLCODE                  PIC -999            VALUE ZEROS.
       01  WRK-NULO                     PIC S9(004) COMP    VALUE ZEROS.
      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '      VARIAVEIS DE ERRO   '.

       01 WRK-PROG-ON                  PIC X(050)          VALUE
           'F2403DB3'.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '           BOOKS          '.
      *----------------------- BOOK DE MENSAGEM DE ERRO ---------------*
       COPY BOOKERRO.
      *----------------------- BOOK DE GRAVACAO DE ERROS --------------*
       COPY BOOKLOG.
      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '      AREA DB2            '.
      *----------------------------------------------------------------*

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC.

           EXEC SQL
              INCLUDE BOOKFUNC
           END-EXEC.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '  AREA CURSOR     '.

           EXEC SQL
              DECLARE DADOS CURSOR FOR
              SELECT IDFUNC,NOMEFUNC,DEPTOFUNC,SALFUNC,FILHOSFUNC,
                     ADMISFUNC
                     FROM F2403.FUNC
           END-EXEC.

      *----------------------------------------------------------------*
       01  FILLER                      PIC X(050)          VALUE
           '***** FIM DA WORKING *****'.
      *================================================================*
       PROCEDURE                       DIVISION.
      *================================================================*

       0000-PRINCIPAL                  SECTION.

           PERFORM 0100-INICIALIZAR.
           PERFORM 0200-PROCESSAR UNTIL SQLCODE EQUAL 100.
           PERFORM 0300-FINALIZAR.
           GOBACK.

       0000-PRINCIPAL-FIM.             EXIT.
      *----------------------------------------------------------------*
       0100-INICIALIZAR                SECTION.

           EXEC SQL

              OPEN DADOS

           END-EXEC.

           PERFORM 0110-TESTAR-SQL.

       0100-INICIALIZAR-FIM.           EXIT.
      *----------------------------------------------------------------*
       0110-TESTAR-SQL                 SECTION.

           IF SQLCODE                  NOT EQUAL 0 AND NOT EQUAL 100

              MOVE SQLCODE             TO WRK-SQLCODE
              MOVE WRK-PROG-ON         TO WRK-PROGRAMA
              MOVE '0110'              TO WRK-SECAO
              MOVE WRK-ERRO-ABERTURA   TO WRK-MENSAGEM
              MOVE WRK-SQLCODE         TO WRK-STATUS
              PERFORM 9999-TRATA-ERRO

           END-IF.

       0110-TESTAR-SQL-FIM.            EXIT.
      *----------------------------------------------------------------*
       0200-PROCESSAR                  SECTION.

           EXEC SQL
              FETCH DADOS
              INTO :DB2-IDFUNC,
                   :DB2-NOMEFUNC,
                   :DB2-DEPTOFUNC,
                   :DB2-SALFUNC,
                   :DB2-FILHOSFUNC INDICATOR :WRK-NULO,
                   :DB2-ADMISFUNC
           END-EXEC.

           IF WRK-NULO                 EQUAL -1

              MOVE 0                   TO DB2-FILHOSFUNC

           END-IF.

           EVALUATE SQLCODE

              WHEN 0
                  DISPLAY 'NOME DO FUNCIONARIO: ' DB2-NOMEFUNC
                  DISPLAY 'SALARIO            : ' DB2-SALFUNC
                  DISPLAY 'FILHOS             : ' DB2-FILHOSFUNC
                  DISPLAY '----------------------------------'

              WHEN 100

                  DISPLAY 'FIM DA TABELA'

              WHEN OTHER

                  DISPLAY 'ERRO DE LEITURA: ' WRK-SQLCODE


           END-EVALUATE.

       0200-PROCESSAR-FIM.             EXIT.
      *----------------------------------------------------------------*
       0300-FINALIZAR                  SECTION.

           EXEC SQL

              CLOSE DADOS

           END-EXEC.

           IF SQLCODE                  NOT EQUAL 0 AND NOT EQUAL 100

              MOVE SQLCODE             TO WRK-SQLCODE
              MOVE WRK-PROG-ON         TO WRK-PROGRAMA
              MOVE '0300'              TO WRK-SECAO
              MOVE WRK-ERRO-FECHAMENTO TO WRK-MENSAGEM
              MOVE WRK-SQLCODE         TO WRK-STATUS
              PERFORM 9999-TRATA-ERRO

           END-IF.

           GOBACK.

       0300-FINALIZAR-FIM.             EXIT.
      *----------------------------------------------------------------*
       9999-TRATA-ERRO                 SECTION.

           CALL WRK-PROGRAMA-LOGERRO   USING WRK-DADOS.
           GOBACK.

       9999-TRATA-ERRO-FIM.            EXIT.
      *----------------------------------------------------------------*
